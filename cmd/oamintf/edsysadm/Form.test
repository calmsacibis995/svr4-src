#ident	"@(#)oamintf:edsysadm/Form.test	1.5.1.1"

################################################################################
#	Module Name: Form.test
#	Date: March 1988
#
#	ARGS PASSED IN:
#			ARG1 -> menu/task flag
#			ARG2 -> package description file name
#			ARG3 -> menu/task name
#			ARG4 -> menu/task description
#			ARG5 -> menu/task location
#			ARG6 -> menu/task help
#			ARG7 -> task action file (TASK ONLY)
#			ARG8 -> comma separated task files (TASK ONLY)
#			ARG9 -> original location (LOCATION:NAME)
#
#	ARGS PASSED OUT:
#			ARG1 -> menu/task flag
#			ARG2 -> package description file name
#			ARG3 -> menu/task name
#			ARG4 -> menu/task description
#			ARG5 -> menu/task location
#			ARG6 -> menu/task help
#			ARG7 -> task action file
#			ARG8 -> comma separated task files
#			ARG9 -> original location (LOCATION:NAME)
################################################################################
form="On-line Testing"

framemsg=`readfile $INTFBASE/form.msg`

help=OPEN TEXT $INTFBASE/Text.itemhelp $LININFO

done=`
      if [ "$F1" = "yes" ];
      then 
	    indicator -w;

	    test_menu "$ARG1" "$ARG3" "$ARG4" "$ARG5" "$ARG6" > /dev/null;

	    if [ $RET -eq 0 ];
	    then
	        set -l CMD="OPEN MENU $EDSYSADM/Menu.testmenu";
	    else
	        set -l CMD="OPEN TEXT $EDSYSADM/Text.tmerr $RET $ARG3";
	    fi;
      else if [ "$F1" = "no" ];
      then
	   set -l CMD="OPEN FORM $EDSYSADM/Form.save \"$ARG1\" \"$ARG2\"
		       \"$ARG3\" \"$ARG4\" \"$ARG5\" \"$ARG6\" \"$ARG7\" \"$ARG8\" \"$ARG9\"";
	   fi;
      fi`$!CMD

name="Do you want to test this modification before continuing?"
lininfo=Form.test:test
nrow=1
ncol=3
frow=1
fcol=60
rows=1
columns=3
value=yes

rmenu={ yes no }
menuonly=true

invalidmsg=const "Error: only choices are yes or no."
choicemsg=const "Enter yes or no."

name=RESET
button=8
action=reset
