#ident	"@(#)oamintf:devices/copy/Form.diskette	1.7.4.1"

################################################################################
#	Module Name: Form.diskette
################################################################################
form="Copying Volumes"

framemsg=`readfile $INTFBASE/form.msg`

help=OPEN TEXT $INTFBASE/Text.itemhelp $LININFO

done=`set -l CPFILE=/tmp/${VPID}.cp;

	if [ "$F2" = "$F3" ];
	then
	   set -l CMD="OPEN TEXT $OBJ_DIR/Text.copy1 $F2";
	else
	   set -l CMD="OPEN TEXT $OBJ_DIR/Text.copy3 $F2 $F3";
	fi`$CMD

name=Device group:
lininfo=devgrp
nrow=1
ncol=1
frow=1
fcol=17
rows=1
columns=18
show=`showgrp diskette;

	if [ "$RET" -eq 0 ];
	then
	  echo true;
	else
	  echo false;
	fi`

rmenu={ `getdgrp type=diskette | sort` }
valid=`getdgrp type=diskette $F1 | grep diskette > /dev/null;
	if [ "$RET" -eq 0 ];
	then
		echo true;
	else
		echo false;
	fi`

invalidmsg=const "ERROR: Invalid device group entered."

name=Device drive for removable volume to be copied:
lininfo=devdr1
nrow=2
ncol=1
frow=2
fcol=49
rows=1
columns=18

rmenu=OPEN MENU $OBJ_DIR/../Menu.drive "$F1" diskette
valid=`valdrive "$F1" diskette "$F2"`

invalidmsg=const "ERROR: Invalid device entered."

name=Device drive for volume on which copy will be made:
lininfo=devdr2
nrow=3
ncol=1
frow=3
fcol=53
rows=1
columns=18
value=$F2

rmenu=OPEN MENU $OBJ_DIR/../Menu.drive $F1 diskette
valid=`valdrive "$F1" diskette "$F3"`

invalidmsg=const "ERROR: Invalid device entered."

name=RESET
button=8
action=RESET
